import com.android.Version
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.gradle.api.attributes.Attribute

import java.nio.file.Paths

buildscript {
   ext.safeExtGet = {prop ->
      rootProject.ext.has(prop) ? rootProject.ext.get(prop) : project.properties['ApiVideoLiveStream_' + prop]
  }
  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:8.1.1"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${safeExtGet('kotlinVersion')}"
  }
}

def getExtOrDefault(name) {
  if (rootProject.ext.has(name)) {
    return rootProject.ext.get(name)
  }
  def lowerKey = "ApiVideoLivestream_${name}"
  if (project.hasProperty(lowerKey)) {
    return project.properties[lowerKey]
  }
  def camelKey = "ApiVideoLiveStream_${name}"
  if (project.hasProperty(camelKey)) {
    return project.properties[camelKey]
  }
  return null
}

def getExtOrIntegerDefault(prop) {
  def value = getExtOrDefault(prop)
  if (value == null) {
    def fallbacks = [
      compileSdkVersion: 35,
      minSdkVersion    : 24,
      targetSdkVersion : 35
    ]
    return fallbacks[prop].toInteger()
  }
  return value.toInteger()
}

static def findNodeModulePath(baseDir, packageName) {
    def basePath = baseDir.toPath().normalize()
    // Node's module resolution algorithm searches up to the root directory,
    // after which the base path will be null
    while (basePath) {
        def candidatePath = Paths.get(basePath.toString(), "node_modules", packageName)
        if (candidatePath.toFile().exists()) {
            return candidatePath.toString()
        }
        basePath = basePath.getParent()
    }
    return null
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

static def supportsNamespace() {
  def parsed = Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
  def major = parsed[0].toInteger()
  def minor = parsed[1].toInteger()

  // Namespace support was added in 7.3.0
  if (major == 7 && minor >= 3) {
    return true
  }

  return major >= 8
}

apply plugin: 'com.android.library'
apply plugin: 'com.facebook.react'
apply plugin: 'kotlin-android'

android {
  if (supportsNamespace()) {
    namespace "video.api.reactnative.livestream"

    sourceSets {
      main {
        manifest.srcFile "src/main/AndroidManifestNew.xml"
      }
    }
  }

  ndkVersion (getExtOrDefault("ndkVersion") ?: "27.1.12297006")
  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_17
    targetCompatibility JavaVersion.VERSION_17
  }

  buildFeatures {
    buildConfig true
  }

  kotlinOptions {
    jvmTarget = "17"
  }

  sourceSets {
    main {
      jniLibs.srcDirs = ["src/main/jniLibs"]
      if (isNewArchitectureEnabled()) {
        java.srcDirs += ["src/newarch"]
      } else {
        java.srcDirs += ["src/oldarch"]
      }
    }
  }

  packaging {
    jniLibs {
      pickFirsts += ["**/librtmpdroid.so"]
    }
  }
}

kotlin {
  jvmToolchain {
    languageVersion.set(JavaLanguageVersion.of(17))
  }
  compilerOptions {
    jvmTarget.set(JvmTarget.JVM_17)
  }
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
  kotlinOptions {
    jvmTarget = "17"
  }
}

def reactNativePath = findNodeModulePath(projectDir, "react-native")
def codegenPath = findNodeModulePath(projectDir, "@react-native/codegen")
if (codegenPath == null) {
  // Compat for 0.71 and lower (to be removed)
  codegenPath = findNodeModulePath(projectDir, "react-native-codegen")
}

repositories {
    if (reactNativePath) {
        maven {
            url "${reactNativePath}/android"
        }
    }
    mavenCentral()
    google()
}

dependencies {
  // For < 0.71, this will be from the local maven repo
  // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
  //noinspection GradleDynamicVersion
  if (reactNativePath) {
    implementation "com.facebook.react:react-native:+"
  } else {
    implementation "com.facebook.react:react-android:${getExtOrDefault('reactNativeVersion') ?: '0.73.6'}"
  }

  implementation("video.api:android-live-stream:1.4.3") {
    exclude group: 'video.api', module: 'rtmpdroid'
    // exclude the transitive dependency to use packed version to avoid conflict with libssl.so and libcrypto.so
  }
  // Use compileOnly so the JAR is available at compile time but not bundled in the AAR
  // The app module will include the JAR directly to avoid duplicate classes
  compileOnly(files("libs/rtmpdroid-1.0.2.jar"))
  implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
}

if (isNewArchitectureEnabled()) {
  react {
    jsRootDir = file("../src")
    libraryName = "ApiVideoLiveStreamView"
    codegenJavaPackageName = "video.api.reactnative.livestream"
  }
}
